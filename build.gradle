plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
	id 'com.matthewprenger.cursegradle' version '1.+'
	id "com.modrinth.minotaur" version "2.+"
}

//archivesBaseName = project.archives_base_name
version = project.minecraft_version + "-" + project.mod_version
group = project.maven_group

repositories {
	maven { name="BuildCraft"; url="https://mod-buildcraft.com/maven" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url 'https://jitpack.io' }
	maven { url "https://maven.terraformersmc.com/releases" }
	maven { url "https://maven.pkom.ml/" }
	mavenLocal()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi "alexiil.mc.lib:libblockattributes-all:$libblockattributes_version"
	modApi "alexiil.mc.lib:libnetworkstack-base:$libnetworkstack_version"
	modApi "alexiil.mc.lib:libmultipart-base:$libmultipart_version"

	include "alexiil.mc.lib:libblockattributes-all:$libblockattributes_version"
	include "alexiil.mc.lib:libnetworkstack-base:$libnetworkstack_version"
	include "alexiil.mc.lib:libmultipart-base:$libmultipart_version"

	modImplementation "alexiil.mc.mod:simplepipes-all:${rootProject.simplepipes_version}"

	modImplementation "ml.pkom:mcpitanlibarch-fabric${rootProject.mcpitanlib_version}"

	// Mod Menu
	modImplementation "com.terraformersmc:modmenu:${rootProject.modmenu_version}"

	// Cloth Config API
	modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.clothconfig_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include "me.shedaniel.cloth:cloth-config-fabric:${rootProject.clothconfig_version}"
}

curseforge {
	if (System.getenv("CURSEFORGE_TOKEN") != null) {
		apiKey = System.getenv("CURSEFORGE_TOKEN")
		project {
			id = '514438'
			changelog = 'port to 1.20'
			releaseType = 'beta'
			addGameVersion '1.20'
			addGameVersion '1.20.1'
			addGameVersion "Fabric"
			relations {
				requiredDependency 'fabric-api'
				requiredDependency 'mcpitanlibarch'
				requiredDependency 'simplepipes'
				embeddedLibrary 'cloth-config'
			}

			mainArtifact(remapJar)
		}
		options {
			forgeGradleIntegration = false
		}
	}
}

import com.modrinth.minotaur.dependencies.ModDependency

modrinth {
	if (System.getenv("MODRINTH_TOKEN") != null) {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = 'zSS7GIKK'
		versionNumber = project.mod_version
		uploadFile = remapJar
		gameVersions = ['1.20', '1.20.1']
		versionType = 'beta'
		loaders = ['fabric']
		changelog = 'update for latest simple pipes'
		dependencies = [
				new ModDependency('P7dR8mSH', 'required'), // Fabric API
				new ModDependency('uNRoUnGT', 'required'),  // MCPitanLibarch
				new ModDependency('ls90k6B8', 'required'),  // MCPitanLibarch
				new ModDependency('9s6osm5g', 'embedded')  // MCPitanLibarch
		]
	}
}

processResources {
	inputs.property "version", project.version
	filteringCharset "UTF-8"

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"
	if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
		it.options.release = targetJavaVersion
	}
}

java {
	def javaVersion = JavaVersion.toVersion(targetJavaVersion)
	if (JavaVersion.current() < javaVersion) {
		toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
	}
	archivesBaseName = project.archives_base_name
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
